resource "aws_ecs_cluster" "main" {
  name = "${var.project_name}-cluster"
}

resource "aws_ecs_task_definition" "backend" {
  family                   = "${var.project_name}-backend"
  network_mode             = "awsvpc"
  requires_compatibilities = ["FARGATE"]
  cpu                      = "1024"
  memory                   = "2048"
  execution_role_arn       = aws_iam_role.ecs_task_execution.arn
  task_role_arn            = aws_iam_role.ecs_task.arn

  container_definitions = jsonencode([
    {
      name      = "backend"
      image     = var.backend_image
      essential = true
      portMappings = [{ containerPort = 5000 }]
      environment = [
        { name = "AWS_REGION", value = var.aws_region },
        { name = "DYNAMODB_KEYS_TABLE", value = aws_dynamodb_table.user_keys.name },
        { name = "DYNAMODB_CHAT_TABLE", value = aws_dynamodb_table.chats.name },
        { name = "FERNET_KEY", value = aws_ssm_parameter.fernet_key.value },
        { name = "LANGCHAIN_API_KEY", value = aws_ssm_parameter.langchain_api_key.value },
      ]
    }
  ])
}

resource "aws_ecs_service" "backend" {
  name            = "${var.project_name}-backend"
  cluster         = aws_ecs_cluster.main.id
  task_definition = aws_ecs_task_definition.backend.arn
  desired_count   = 2
  launch_type     = "FARGATE"
  network_configuration {
    subnets          = aws_subnet.public[*].id
    security_groups  = [aws_security_group.backend.id]
    assign_public_ip = true
  }
  load_balancer {
    target_group_arn = aws_lb_target_group.backend.arn
    container_name   = "backend"
    container_port   = 5000
  }
  depends_on = [aws_lb_listener.backend]
} 